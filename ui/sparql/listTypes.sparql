PREFIX rdf:	<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX bio:	<http://purl.org/vocab/bio/0.1/>
PREFIX crm:	<http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dcmitype:	<http://purl.org/dc/dcmitype/>
PREFIX dcterms:	<http://purl.org/dc/terms/>
PREFIX foaf:	<http://xmlns.com/foaf/0.1/>
PREFIX geo:	<http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX nm:	<http://nomisma.org/id/>
PREFIX nmo:	<http://nomisma.org/ontology#>
PREFIX org:	<http://www.w3.org/ns/org#>
PREFIX osgeo:	<http://data.ordnancesurvey.co.uk/ontology/geometry/>
PREFIX rdac:	<http://www.rdaregistry.info/Elements/c/>
PREFIX skos:	<http://www.w3.org/2004/02/skos/core#>
PREFIX spatial: <http://jena.apache.org/spatial#>
PREFIX un:	<http://www.owl-ontologies.com/Ontology1181490123.owl#>
PREFIX wordnet: <http://ontologi.es/WordNet/class/>
PREFIX xsd:	<http://www.w3.org/2001/XMLSchema#>

SELECT ?coinType ?label ?source ?sourceLabel ?startDate ?endDate (GROUP_CONCAT(?mint;separator='|') as ?mints) (GROUP_CONCAT(?mintLabel;separator='|') as ?mintLabels) (GROUP_CONCAT(?authority;separator='|') as ?authorities) (GROUP_CONCAT(?authorityLabel;separator='|') as ?authorityLabels) (GROUP_CONCAT(?den;separator='|') as ?dens) (GROUP_CONCAT(?denLabel;separator='|') as ?denLabels) WHERE {
%STATEMENTS%
?coinType skos:prefLabel ?label FILTER(langMatches(lang(?label), "en")) .
   MINUS {?coinType dcterms:isReplacedBy ?replaced}
   ?coinType dcterms:source ?source . 
   	?source skos:prefLabel ?sourceLabel FILTER(langMatches(lang(?sourceLabel), "en"))
   OPTIONAL {?coinType nmo:hasStartDate ?startDate}
   OPTIONAL {?coinType nmo:hasEndDate ?endDate}
   OPTIONAL {?coinType nmo:hasMint ?mint . 
   	?mint skos:prefLabel ?mintLabel FILTER(langMatches(lang(?mintLabel), "en"))}
   OPTIONAL {?coinType nmo:hasAuthority ?authority . 
   	?authority skos:prefLabel ?authorityLabel FILTER(langMatches(lang(?authorityLabel), "en"))}
   OPTIONAL {?coinType nmo:hasDenomination ?den . 
   	?den skos:prefLabel ?denLabel FILTER(langMatches(lang(?denLabel), "en"))}
} GROUP BY ?coinType ?label ?source ?sourceLabel ?startDate ?endDate